group 'tech.slideshare'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.8
mainClassName = 'tech.slideshare.twitter.Main'

applicationDefaultJvmArgs = [
        "-Dtwitter4j.oauth.consumerKey=${project.hasProperty("consumerKey") ? project.consumerKey : ""}",
        "-Dtwitter4j.oauth.consumerSecret=${project.hasProperty("consumerSecret") ? project.consumerSecret : ""}",
        "-Dtwitter4j.oauth.accessToken=${project.hasProperty("accessToken") ? project.accessToken : ""}",
        "-Dtwitter4j.oauth.accessTokenSecret=${project.hasProperty("accessTokenSecret") ? project.accessTokenSecret : ""}"
]

run {
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
}

buildscript {
    ext.kotlin_version = '1.0.5'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    // https://mvnrepository.com/artifact/mysql/mysql-connector-java
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.6'

    // https://mvnrepository.com/artifact/org.twitter4j/twitter4j-core
    compile group: 'org.twitter4j', name: 'twitter4j-core', version: '4.0.3'

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'

    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile 'org.jetbrains.kotlinx:kotlinx-support-jdk8:0.3'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

jar {
    manifest {
        attributes(
                "Main-Class": mainClassName,
                "Class-Path": configurations.compile.collect { "lib/" + it.getName() }.join(' '))
    }
}

task copyLib(type: Copy) {
    from configurations.compile
    into 'build/libs/lib'
}
